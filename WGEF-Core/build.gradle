plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'kr.entree.spigradle' version '2.2.4'
    id "net.kyori.indra.git" version "2.1.1"
}

group 'io.github.invvk'
version "$parent.version"

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly 'net.essentialsx:EssentialsX:2.20.1'
    compileOnly 'me.clip:placeholderapi:2.11.4'

    implementation "org.bstats:bstats-bukkit:3.0.0"
    implementation project(":WGEF-Abstraction")
    implementation project(":WG7")
}

shadowJar {
    archiveBaseName.set('WGEF-REBORN')
    archiveClassifier.set('')
    archiveVersion.set('')

    //PUT THE PATH TO YOUR BUILD DIRECTORY HERE - ex:
    // destinationDirectory = file("C:\\Users\\david\\Desktop\\Server\\plugins")
    destinationDirectory = file(YOUR_PATH_HERE)

    exclude 'META-INF/**'
    exclude 'LICENSE'

    relocate 'org.bstats', 'io.github.davidarthurcole.wgef.metrics'

    finalizedBy('clean', ':WG7:clean', ':WGEF-Abstraction:clean')
}

spigot {
    main 'io.github.davidarthurcole.wgef.WGEFPlugin'
    name 'WGEF-Reborn'
    authors 'oBlazin', 'DavidArthurCole'
    apiVersion '1.20'
    version "${parent.version}+${indraGit.commit().abbreviate(7).name()}"
    depends 'WorldGuard'
    softDepends 'FastAsyncWorldEdit', 'Essentials', 'TAB'
    description 'Adds more flags to WorldGuard to help manage your server easily!'
}